(* Small program to test the ORC writing facility:
 * Requires the string representation of a ramen type as a command line
 * argument, then writes and compiles an ORC writer for that format, then
 * reads from stdin string representation of ramen values and write them,
 * until EOF when it exits (C++ Handler being deleted and therefore the
 * ORC file flushed). *)
open Batteries
open RamenHelpers
open RamenLog
module T = RamenTypes
module Orc = RamenOrc

let test () =
  let rtyp = T.(
    make (TRecord [|
      "u16", make TU16 ;
      "i32", make ~nullable:false TI32 ;
      "ip4", make ~nullable:false TIpv4 ;
      "ip6", make TIpv6 ;
      "ip", make ~nullable:false TIp ;
      "cidr4", make TCidrv4 ;
      "cidr6", make ~nullable:false TCidrv6 ;
      "tuple", make (TTuple [| make ~nullable:false TI8 ; make TI32 |]) ;
      "list", make (TList (make TString)) ;
      "complex", make (TRecord [|
        "i8", make TI8 ;
        "str", make ~nullable:false TString ;
        "tuple", make (TTuple [|
          make ~nullable:false
            (TList (make ~nullable:false (TVec (3, make TU64)))) ;
          make ~nullable:false TCidr
        |])
      |])
    |])) in
  Orc.emit_batch_value "test" rtyp stdout

let main =
  init_logger Debug ;
  let ramen_type = Sys.argv.(1) in
  let rtyp = PPP.of_string_exc T.t_ppp_ocaml ramen_type in
  !logger.info "Parsing type %s"
    (IO.to_string T.print_typ rtyp |> abbrev 130) ;
  (* We need to generate two things from this type:
   * - a string parser (using [CodeGen_OCaml.emit_value_of_string])
   * - a ORC writer (using [Orc.emit_batch_value]).
   * Then we compile this into a program that write stdin into an ORC file. *)
  let xtyp = IO.to_string CodeGen_OCaml.otype_of_type rtyp in
  !logger.info "Corresponding runtime type: %s" xtyp ;
  let otyp = Orc.of_structure rtyp.T.structure in
  let schema = IO.to_string Orc.print otyp in
  !logger.info "Corresponding ORC type: %s" schema ;
  RamenOCamlCompiler.use_external_compiler := false ;
  RamenOCamlCompiler.bundle_dir := Sys.getenv_opt "RAMEN_LIBS" |? "./bundle" ;
  (*
   * Generate the C++ side:
   *)
  let orc_write_func = "orc_write" in
  let cc_src_file =
    Filename.temp_file "orc_writer_cc_" ".cc" in
  mkdir_all ~is_file:true cc_src_file ;
  File.with_file_out cc_src_file (fun oc ->
    Orc.emit_intro oc ;
    Orc.emit_batch_value orc_write_func rtyp oc ;
    Orc.emit_outro oc) ;
  !logger.info "Generated C++ support file in %s" cc_src_file ;
  let cpp_command src dst =
    let _, where = Unix.run_and_read "ocamlc -where" in
    let where = String.trim where in
    Printf.sprintf "g++ -std=c++17 -c -I %S -o %S %S"
      where dst src in
  let cc_dst = change_ext ".o" cc_src_file in
  let cmd = cpp_command cc_src_file cc_dst in
  let status = Unix.system cmd in
  if status <> Unix.WEXITED 0 then (
    !logger.error "Compilation of %S: %s"
      cc_src_file (string_of_process_status status) ;
    !logger.info "I'm so sorry! This is what I tried: %s" cmd ;
    !logger.info "I'm out of ideas. Good luck!" ;
    exit 1) ;
  (*
   * Now the ML side:
   *)
  let ml_obj_name =
    Filename.temp_file "orc_writer_" ".cmx" |>
    RamenOCamlCompiler.make_valid_for_module in
  let keep_temp_files = true in
  let ml_src_file =
    RamenOCamlCompiler.with_code_file_for ml_obj_name keep_temp_files (fun oc ->
      let p fmt = Printf.fprintf oc (fmt^^"\n") in
      p "open Batteries" ;
      p "open Stdint" ;
      p "open RamenHelpers" ;
      p "open RamenNullable" ;
      p "open RamenLog" ;
      p "" ;
      p "let value_of_string str =" ;
      p "  check_parse_all (String.length str) (" ;
      let emit_is_null fins str_var offs_var oc =
        Printf.fprintf oc
          "if looks_like_null ~offs:%s %s &&
            string_is_term %a %s (%s + 4) then \
         true, %s + 4 else false, %s"
        offs_var str_var
        (List.print Char.print) fins str_var offs_var
        offs_var offs_var in
      CodeGen_OCaml.emit_value_of_string 2 rtyp "str" "0" emit_is_null [] oc ;
      p "  )" ;
      p "" ;
      p "(* A handler to be passed to the function generated by " ;
      p "   emit_batch_value: *)" ;
      p "type handler" ;
      p "" ;
      p "external orc_write : handler -> %s -> unit = %S"
        xtyp orc_write_func ;
      (* Destructor do not seems to be called when the OCaml program exists: *)
      p "external orc_close : handler -> unit = \"orc_handler_close\"" ;
      p "" ;
      p "(* Parameters: path * schema * row per batch * batches per file *)" ;
      p "external create_handler : string -> string -> int -> int -> handler =" ;
      p "  \"orc_handler_create\"" ;
      p "" ;
      p "let main =" ;
      p "  let orc_fname = Sys.argv.(1) in" ;
      p "  let handler = create_handler orc_fname %S 1000 100 in" schema ;
      p "  try forever (fun () ->" ;
      p "        read_line () |> value_of_string |> orc_write handler" ;
      p "      ) ()" ;
      p "  with End_of_file ->" ;
      p "    !logger.info \"Exiting...\" ;" ;
      p "    orc_close handler") in
  !logger.info "Generated OCaml support module in %s" ml_src_file ;
  (*
   * Link!
   *)
  let obj_files = [ cc_dst ] in
  let exec_file = Filename.remove_extension ml_src_file in
  RamenOCamlCompiler.link ~debug:true ~keep_temp_files ~what:"ORC writer"
                          ~obj_files ~src_file:ml_src_file ~exec_file ;
  !logger.info "Good luck!"
